// Bot variables
! var number = 1
! var name = notEliza

// A generic set of chatting responses.
> begin
  // If we don't know their name, set the new_user topic and continue.
  + request
  * <get met> == undefined => <set met=true>{topic=new_user}{ok}
  - {ok}
< begin

+ *
- That is interesting. Please continue.
- Tell me more about that.
- <get name> do you want to play the Game 'Guess a Number'? Use the keyword 'play'.

+ (hello|hi|hey|howdy|hola|hai|yo) [*]
* <get name> != undefined => {random}
^ Hello there, <get name>!|
^ Hey, <get name>!{/random}
- Hello there!
- Hi there!
- Hello!

+ my name is *
* <get name> == undefined  => <set name=<formal>>Nice to meet you <get name>!
* <get name> == <formal> => Yes I know, silly!
- <set oldname=<get name>><set name=<formal>>
^ I thought your name was <get oldname>?

+ what is my name
* <get name> == undefined => You never told me your name.
- Your name is <get name>, silly!
- Aren't you <get name>?

+ i am # years old
- <set age=<star>>I will remember that you are <get age> years old.

+ how old am i
* <get age> == undefined => You never told me how old you are.
- You are <get age> years old.

+ [*] do [*]
- We can play a game. Use the keyword 'play' to start.

+ i hate you
- You're really mean! I'm not talking again until you apologize.{topic=sorry}

+ [*] play [*]
- We will play the game Number Guesser <get name>. Type a number between 1 and 10000.{topic=game}

//TOPICS

//Topics new user
> topic new_user
  + *
  - Hi! I'm <bot name>! I'm a chatbot written in RiveScript.\s
  ^ What is your name?{topic=asked_name}
< topic

> topic asked_name
  + #
  - Your name is a number?
  + *
  - I only want your first name.
  + _
  - <set name=<formal>>Nice to meet you, <get name>!{topic=random}
< topic


//Topic sorry
> topic sorry
  + [*] sorry [*]
  - It's OK <get name>, I'll forgive you!{topic=random}

  + *
  - Nope, not until you apologize.
  - Say you're sorry!
  - Apologize!
< topic

//Topic: Play
> topic game

+ init #
- <set random=<star>> random value saved

+ get random
- <get random> is your current random value

+ #
* <star> >  <get random> => Pick a lower number!
* <star> <  <get random> => Pick a higher number!
* <star> == <get random> => Congrats, you're right! You may ask to play again.{topic=random}

+ [*] quit [*]
- Ok Game Over.{topic=random}

< topic